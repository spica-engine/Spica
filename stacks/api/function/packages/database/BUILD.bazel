package(default_visibility = ["//visibility:public"])

load("@npm//@bazel/typescript:index.bzl", "ts_library")
load("@build_bazel_rules_nodejs//:index.bzl", "pkg_npm")
load("@npm//@bazel/rollup:index.bzl", "rollup_bundle")

SRCS = glob(["src/*.ts"]) + ["index.ts"]

ts_library(
    name = "database",
    srcs = SRCS,
    module_name = "@spica-devkit/database",
    tsconfig = "//stacks/api/function/packages:tsconfig",
    deps = [
        "@npm//@types/mongodb",
        "@npm//@types/node",
        "@npm//mongodb",
    ],
)

ROLLUP_DEPS = [
    ":database",
    "@npm//rollup-plugin-commonjs",
    "@npm//rollup-plugin-node-resolve",
    "@npm//@rollup/plugin-json",
]

rollup_bundle(
    name = "cjs",
    config_file = "//stacks/api/function/packages:rollup.config.js",
    entry_points = {
        "index.ts": "index.js",
    },
    format = "cjs",
    output_dir = True,
    sourcemap = "true",
    deps = ROLLUP_DEPS,
)

rollup_bundle(
    name = "esm",
    config_file = "//stacks/api/function/packages:rollup.config.js",
    entry_points = {
        "index.ts": "index.mjs",
    },
    format = "esm",
    output_dir = True,
    sourcemap = "true",
    deps = ROLLUP_DEPS,
)

genrule(
    name = "bundle",
    srcs = [
        ":esm",
        ":cjs",
        ":database",
    ],
    outs = [
        "dist/index.js",
        "dist/index.js.map",
        "dist/index.mjs",
        "dist/index.mjs.map",
    ] + [
        "dist/%s.d.ts" % (f[:-3])
        for f in SRCS
    ],
    cmd = "cp -r $(locations :cjs)/* $(RULEDIR)/dist && cp -r $(locations :esm)/* $(RULEDIR)/dist && " + "&&".join([
        "cp -r $(GENDIR)/stacks/api/function/packages/database/%s.d.ts $(RULEDIR)/dist/%s.d.ts" % (
            f[:-3],
            f[:-3],
        )
        for f in SRCS
    ]),
)

pkg_npm(
    name = "package",
    srcs = [
        "package.json",
        "scripts/postinstall.js",
    ],
    tags = ["release-with-spica"],
    deps = [
        ":bundle",
        ":database",
    ],
)
